plugins {
    id 'org.jetbrains.intellij' version "0.4.18"
}

apply plugin: 'idea'

def jdkVersion = 1.8

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
}

patchPluginXml {
    changeNotes """
        <ul>
            <li>Reworked documentation, moved from wiki to the docs directory</li>
            <li>Add an architecture documentation to explain some of the internal details</li>
            <li>#247 - Duplicated file templates</li>
            <li>#205 - Functors are now indexed (completion and resolution should work)</li>
            <li>#254 - Make a distinction between single-line and multi-line comments in parser</li>
            <li>#254 - Fold multiline JSX tags</li>
            <li>Add github action for CI (and badges)</li>
            <li>#253 - Breaking apart modules to avoid extra instances of IntelliJ when testing</li>
            <li>#257 - Fix monorepo executable</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
}

allprojects {
    group 'com.reason'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.intellij'

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    repositories {
        mavenCentral()
    }

    dependencies {
        def powerMockVersion = '2.0.2'
        testCompile 'org.mockito:mockito-core:3.3.3'
        testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
        testCompile "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

project(':idea-plugin') {
    dependencies {
        compile project(':jps-plugin')
    }
}

/*
git log --oneline | Select-String -Pattern "(191|192|193) -" | %{$_.Line.Split(" ")[0]}
*/
