plugins {
    // https://github.com/JetBrains/gradle-intellij-plugin
    id 'org.jetbrains.intellij' version "0.5.0"
    // https://github.com/sherter/google-java-format-gradle-plugin
    id 'com.github.sherter.google-java-format' version '0.8'
}

repositories {
    jcenter()
}

apply plugin: 'idea'

def jdkVersion = 1.8

idea {
    project {
        jdkName = jdkVersion
        languageLevel = jdkVersion
    }
}

allprojects {
    group 'com.reason'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.intellij'

    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    repositories {
        mavenCentral()
    }

    dependencies {
        def powerMockVersion = '2.0.2'
        testCompile 'org.mockito:mockito-core:3.3.3'
        testCompile "org.powermock:powermock-module-junit4:${powerMockVersion}"
        testCompile "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    }

    patchPluginXml {
        sinceBuild = pluginSinceBuild
        untilBuild = pluginUntilBuild
        changeNotes """
        <ul>
            <li>#272 - Another case: structure panel missing function definition with "and"</li>
            <li>#274 - Omit '()' in structure panel</li>
            <li>#273 - Some constructor names not highlighted in function/match patterns</li>
            <li>#119 - Support for automatically closing quotes (double)</li>
            <li>'Goto class' use a custom presentation for inner modules</li> 
            <li>#91 - Jumping to some module-related symbol definitions from Structure Panel doesn't highlight symbol right away (incorrect module type detection)<li>f
            <li>#271 - Structure panel missing function defined with "and"</li>
            <li>#270 - Function definition with type variables not recognized as a function</li>
            <li>Fix monorepo on windows</li>
        </ul>

        <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>

        <p/>
        <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
    """
    }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources', 'plugins/resources'
        }
        test {
            java.srcDir 'tests'
        }
    }

    task testCompilation(type: Test, group: 'Verification', dependsOn: [classes, testClasses]) {
        testLogging {
            exceptionFormat = 'full'
        }
    }
}

project(':idea-plugin') {
    dependencies {
        compile project(':jps-plugin')
    }
}
