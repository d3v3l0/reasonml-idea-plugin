plugins {
    id 'org.jetbrains.intellij' version '0.7.2'  // https://github.com/JetBrains/gradle-intellij-plugin
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.intellij'

    sourceCompatibility = '11.0.10'
    version = pluginVersion + '-' + platformVersion
    compileJava.options.encoding = 'UTF-8'
    group 'com.reason'

    dependencies {
        def powerMockVersion = '2.0.2'
        testImplementation 'org.mockito:mockito-core:3.3.3'
        testImplementation "org.powermock:powermock-module-junit4:${powerMockVersion}"
        testImplementation "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    }

    sourceSets {
        main {
            java.srcDirs 'src'
            resources.srcDirs 'resources'
        }
        test {
            java.srcDirs 'tests'
        }
    }

    intellij {
        pluginName = pluginName
        type = platformType
        version = platformVersion
        plugins = [
                'java',
                'JavaScriptLanguage',
                'com.jetbrains.hackathon.indices.viewer:' + indicesVersion,
                'PsiViewer:' + psiVersion
        ]

        patchPluginXml {
            sinceBuild = pluginSinceBuild
            untilBuild = pluginUntilBuild
            changeNotes """
                <ul>
                    <li>#315 - insertion handler for JSX doesn't detect close tag</li>
                    <li>#190 - show nested function definitions in structure panel</li>
                    <li>fix incorrect identification of optional parameter</li>
                    <li>order jsx attributes, mandatory first</li>
                </ul>
                <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>
                <p/>
                <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
            """
        }
    }

    verifyPlugin {
        pluginDirectory 'resources'
    }
}

project(':jps-plugin') {
    jar.archiveFileName = "jps-plugin.jar"
    patchPluginXml.enabled = false
}

dependencies {
    implementation project(':jps-plugin')
}
