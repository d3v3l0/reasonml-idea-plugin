plugins {
    id 'org.jetbrains.intellij' version '0.6.5'  // https://github.com/JetBrains/gradle-intellij-plugin
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'org.jetbrains.intellij'

    sourceCompatibility = 1.8
    version = pluginVersion + '-' + platformVersion
    compileJava.options.encoding = 'UTF-8'
    group 'com.reason'

    dependencies {
        def powerMockVersion = '2.0.2'
        testImplementation 'org.mockito:mockito-core:3.3.3'
        testImplementation "org.powermock:powermock-module-junit4:${powerMockVersion}"
        testImplementation "org.powermock:powermock-api-mockito2:${powerMockVersion}"
    }

    sourceSets {
        main {
            java.srcDirs 'src', 'gen'
            resources.srcDirs 'resources'
        }
        test {
            java.srcDirs 'tests'
        }
    }

    intellij {
        pluginName = pluginName
        type = platformType
        version = platformVersion
        plugins = [
                'java',
                'JavaScriptLanguage',
                'com.jetbrains.hackathon.indices.viewer:' + indicesVersion,
                'PsiViewer:' + psiVersion
        ]

        patchPluginXml {
            sinceBuild = pluginSinceBuild
            untilBuild = pluginUntilBuild
            changeNotes """
                <ul>
                    <li>#304 - Properties in component using external are not found</li>
                    <li>Keep caret at position when reformat</li>
                    <li>Fix file pattern introduced in bs-platform 8.3</li>
                    <li>Fix reformatOnSave when multiple projects (different settings) are opened</li>
                    <li>Update ErrorAnnotator to use new ninja format introduced since bs-platform 8.4</li>
                    <li>Update ppx error extraction</li>
                </ul>
                <p><a href="https://github.com/reasonml-editor/reasonml-idea-plugin/blob/master/CHANGELOG.md">Full change log...</a></p>
                <p/>
                <p>To see how to integrate reason tools (bsc, refmt), go to the website.</p>
            """
        }
    }

    verifyPlugin {
        pluginDirectory 'resources'
    }
}

project(':jps-plugin') {
    jar.archiveFileName = "jps-plugin.jar"
    patchPluginXml.enabled = false
}

dependencies {
    implementation project(':jps-plugin')
}
